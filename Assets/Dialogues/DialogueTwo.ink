VAR path1_done = false
VAR path2_done = false
VAR path3_done = false

VAR path4_done = false
VAR path5_done = false
VAR path6_done = false

VAR characterName = ""
VAR characterExpression = 1

VAR bgName = "black"
VAR alinaVisible = false
VAR ghostVisible = false
VAR guilt = false

VAR chose_to_touch = false

-> hub

=== hub ===
~bgName = "chapter2_no_doors"
~ghostVisible = false

{ path1_done and path2_done:
    -> chapter2
- else:
    Ты стоишь перед двумя дверьми. Какой выбрать?

    + {not path1_done} [Пойти в правую дверь]
        -> path1
    + {not path2_done} [Пойти в левую дверь]
        -> path2
}

=== path1 ===
~bgName = "black"
Я зашел в дверь, испытывая предвкушение и неуверенность.
~bgName = "chapter2_class_room_horror"
Я оказался в месте похожим на школьный кабинет. 
Кажется это мой класс, ну или был когда то...
Но он стал каким-то мрачным.
Веет холодок. 
В этом месте все и началось? 
Снова вспоминаю как засыпал на уроках, словно возвращаюсь в те года. 
Вы подходите к парте.
На парте выцарапно имя "Алина"
Алина? Мое сердце пронзила боль, а из глаз потекли слезы...
Но... Почему? 
Я начинию вспоминать
Милую, одинокую девушку, которая была очень близка мне.
Но лицо ее оставалось как в тумане, 
Она любила рисовать и была замкнута.
Кроме меня у нее не было друзей.
Она была немного странная и часто делилась своими мыслями.
Но в один момент... 
~characterName = ""
Я замечаю рядом письмо, оно было адресовано ей.

#letter_show
...

#letter_hide

~characterName = "Я"
Это мой почерк...
После того, как оставил ей это письмо, я ее больше не видел и, позже, узнал что она пропала.
Теперь я сам оказался в этих местах, возможно это было правда?.. 

~characterName = ""
Письмо исчезает у меня в рукахх

~characterName = "???"
~characterExpression = 1
~ghostVisible = true

Это было последнее что ты сказал, после этого она исчезла навсегда.
Она осталась  со своими кошмарами, совсем одна, а ее спаситель дрожал от страха. 
+ [Я каждую ночь помнил ее просьбы помочь...]
    -> yes_path1
+ [Я не виноват в этом.]
    -> no_path1

=== yes_path1 ===
~characterExpression = 0
Теперь ты веришь в ее слова, оказавшись в кошмаре?

~ghostVisible = false
~characterName = ""
~path1_done = true
-> return_to_hub

=== no_path1 ===
~characterExpression = 0
...
#screamer_show
...
#screamer_hide
~ghostVisible = false
~characterName = ""
~path1_done = true
-> return_to_hub

=== path2 ===
~bgName = "chapter2_med_room_horror"

Запах антисептика. Холодный свет. Пустота...

Слишком знакомо. Слишком чуждо одновременно.

-> palata 
=== palata ===
Ты стоишь в старой больничной палате. Тишина почти звенит в ушах.

+ [Осмотреть кровать]
    Простыня смятая. Как будто кто-то бежал... или больше не встал.
    #braclet_show
    На краю лежит старый браслет надписью — "Пациент №14"
    ... 
    Дальше надпись зачеркнута.
    #braclet_hide
    -> palata

+ [Осмотреть прикроватную тумбочку]
    Пусто. Только старый пузырёк с лекарствами. Полупрозрачные капсулы внутри.
    Одна таблетка разбита. Остальное покрыто пылью.
    -> palata

+ [Прочитать записку врача]
    "Пациентка проявляет признаки прогрессирующего расстройства восприятия. Частые жалобы на "говорящие стены" и "комнаты, что не отпускают". Не признаёт, что находится в больнице. Просит кого-то по имени..."
    Имя вычеркнуто. Но я знаю, о ком речь.
    -> palata

+ [Сесть на край кровати]
    Я не пришёл тогда. Она писала мне. Кричала. А я молчал.
    -> ghost_med_room


=== ghost_med_room ===
~ghostVisible = true

Непонятное существо появляется в углу. Оно будто возникла из воздуха, как дым.

~characterName = "???"
Зачем ты пришёл сюда?..

+ [Я пришёл за тобой]
    ~characterExpression = 0
    ~characterName = ""
    Тень смотрит долго, не отводя взгляда
    ~characterName = "???"
    ...
    Поздно. Но... ты всё-таки пришёл.
    -> final_med_room

+ [Я ищу выход]
    ...
    Всегда ищешь выход. Даже из правды.
    -> final_med_room

+ [Промолчать]
    ~characterExpression = 2
    ...
    ....
    Так же, как тогда.
    -> fake_screamer

=== final_med_room ===
~characterName = ""
Слова повисают в воздухе. Но что-то меняется. Словно я сделал шаг. Пусть даже маленький.
~ghostVisible = false
~path2_done = true
-> return_to_hub

=== fake_screamer ===
~characterName = "???"
...
....
.....
#screamer_show
-> real_screamer


=== real_screamer ===
...
#screamer_hide
~ghostVisible = false
~characterName = ""
~path2_done = true
-> return_to_hub



=== path3 ===
~bgName = "chapter2_room_mother_horror"

Запах плесени и влажной пыли. Комната будто жива... но не так, как раньше.

Ты входишь в старую гостиную. Всё кажется знакомым, но что-то не так. Обои вспухшие, мебель чуть перекошена, как будто за ней кто-то стоит.

Это был дом, в котором я когда-то чувствовал тепло... теперь — только тревога.

Я увидел на столе рамку от картины.

Взглянув поближе я заметил что за ней находится фрагмент этой картины.

На этом фрагменте явно чье-то изображение, но сложно понять что это за человек..

Возможно в этой комнате я смогу найти оставшиеся части картины.

Мне стоит поискать.

-> chapter2_house
=== chapter2_house ===
{ path4_done and path5_done and path6_done:
    -> path3_continue
- else:
    Где стоит поискать остальные фрагменты?
    + {not path4_done} [Осмотреть старый портрет на стене]
        -> path4
    + {not path5_done} [Осмотреть диван]
        -> path5
    + {not path6_done} [Осмотреть окно] 
        -> path6
}
=== path4 ===
    Эта картина знакома мне с детства.  
    Женщина на ней — кто-то из прошлого, кто оставил след в моей памяти.  
    Рядом второе лицо — смутное и неясное.
    Я долго смотрел на неё, пытаясь понять, почему она вызывает у меня тревогу.  
   #fragment_show
   ...
   #fragment_hide
    За картиной я смог отыскать фрагмент.
    ~path4_done = true
    -> chapter2_house

=== path5 ===
    Диван кажется заброшенным, на нём много пыли и пятен.  
    Пальцы нащупали под одной из подушек какой-то предмет — фрагмент, который я искал.  
    #fragment_show
    ...
    #fragment_hide
    Это словно маленький ключик к воспоминаниям, которые начинают медленно возвращаться.
    ~path5_done = true
    -> chapter2_house
    
=== path6 ===
    Окно плотно закрыто, но на стекле видны отпечатки пальцев и царапины.
    Это окно мне знакомо.
    Мне нравилось прятать разные вещи за этим поддокоником с близким мне человеком.
    Но я не могу вспомнить с кем...
    Я протинул руку и прощупал фрагмент.
    #fragment_show
    ...
    #fragment_hide

    ~ path6_done = true
   -> chapter2_house   

=== path3_continue ===
Видимо я уже собрал все фрагменты
Мне нужно вернуться к столику с рамкой и собрать изображение.

 +[Соединить фрагменты]
#key_show
...
    ---> chapter2_next
    
=== chapter2_next ===

Я собрал картину.
На ней появилось изображение девушки.
Такое теплое и нежное что у меня сжалось сердце, это была та которую я безвозвратно потерял по своей глупости.
На изображении она выглядела взрослее, как мне хотелось увидеть ее снова.
Я вспомнил о просьбах спасти ее, неужели она находится в этом месте? 
Получается то существо, похожая на нее, что-то знает об этом? 
Если бы я раньше не боялся, я бы не потерял ее, но возможно теперь у меня есть шанс
#key_hide
...
~ghostVisible = true
~characterExpression = 2
Существо появляется передо мной.
~characterName = "???"
Мне снова страшно... Остаться одной. Пожалуйста... прикоснись ко мне.

+ [Прикоснуться]
    ~characterExpression = 0
    ~characterName = ""
    Я осмелился протянуть руку темному силуэту похожему на Алину. 
    Это существо выглядела жутко, но я не мог снова оставить ее.
    ~characterName = "???"
    "Она" многие годы могла лишь мечтать об этом, сущность выглядит счастливой.
    Ты близко осталось совсем немного.
    ~chose_to_touch = true
    -> final_mother_room


+ [Отшатнуться, испугавшись]
    ...
    ....
    Вот так и тогда.
    Ты снова отвернулся.
    ~chose_to_touch = false
    -> final_mother_room

=== final_mother_room ===
~bgName = "black"
Комната затихает. Но я чувствую — она оставила след. Или я только сейчас его заметил.
~path3_done = true
-> chapter2_end


=== return_to_hub ===
~bgName = "chapter2_no_doors"
Ты возвращаешься на развилку...
-> hub

=== chapter2 ===
~bgName = "chapter2"
~ghostVisible = false
Вдруг появляется третья дверь..
Стоит ли туда идти?
 +[Пройти в центральную дверь]
-> path3

===chapter2_end===
    {chose_to_touch:
    ~bgName = "chapter2"
    ~ghostVisible = false
     Тень исчезает и за ней появляется дверь.
       +[Войти]
    #loadScene IntroScene3Happy
    -> END
- else:
    ~bgName = "chapter2"
    ~ghostVisible = false
     Тень кричит и не дает тебе пройти.
       +[Войти]
    #loadScene IntroScene3Bad
    -> END
}